import * as c from "../../libc.ljs"
import { cast } from "ljs:mem"

struct SQHeader {
    /**
     *  The header string: "SQLite format 3\000"
     */
    header_string: u8[16]
    /**
     * The database page size in bytes. Must be a power of two between
     * 512 and 32768 inclusive, or the value 1 representing a page size
     * of 65536.
     */
    page_size: u16
    /**
     * File format write version. 1 for legacy; 2 for WAL.
     */
    write_file_format: u8
    /**
     * File format read version. 1 for legacy; 2 for WAL.
     */
    read_file_format: u8
    /**
     * Bytes of unused "reserved" space at the end of each page. Usually 0.
     */
    page_end_reserved_bytes: u8
    /**
     * Maximum embedded payload fraction. Must be 64.
     */
    max_embedded_payload_fraction: u8
    /**
     * Minimum embedded payload fraction. Must be 32.
     */
    min_embedded_payload_fraction: u8
    /**
     * Leaf payload fraction. Must be 32.
     */
    leaf_payload_fraction: u8

    /**
     * File change counter.
     */
    file_change_counter: u32
    /**
     *  Size of the database file in pages. The "in-header database size".
     */
    page_count: u32
}

const s = c.str
function main(): c.int {
    c.printf(s`SQLite file reader\n`)
    c.printf(s`==================\n`)
    const file = c.fopen(s`./djs_ljs/examples/sqlite_file_reader/test.sqlite`, s`r`)
    if (file === null) {
        c.perror(s`Couldn't open file`)
        c.abort()
    }

    const fd = c.fileno(file)

    const ptr = c.mmap(
        null,
        100,
        c.PROT_READ,
        c.MAP_PRIVATE,
        fd,
        0
    )
    if (ptr === null) {
        c.perror(s`Couldn't mmap`)
        c.abort()
    }
    const header = cast<*SQHeader>(ptr)
    const header_str = cast<*c.char>(header)
    c.puts(header_str)
    c.printf(s`DB page size: %d\n`, header.page_size)
    c.printf(s`Write file format: %s (%d)\n`, file_format_to_str(header.write_file_format), header.write_file_format)
    c.printf(s`Read file format: %s (%d)\n`, file_format_to_str(header.read_file_format), header.read_file_format)
    c.printf(s`Bytes reserved after every page: %d\n`, header.page_end_reserved_bytes)
    c.printf(s`Max embedded payload fraction: %d\n`, header.max_embedded_payload_fraction)
    c.printf(s`Min embedded payload fraction: %d\n`, header.min_embedded_payload_fraction)
    c.printf(s`Leaf payload fraction: %d\n`, header.leaf_payload_fraction)
    c.printf(s`File change counter: %d\n`, header.file_change_counter)
    c.printf(s`Page count: %d`, header.page_count)

    c.fclose(file)
    return 0
}
function file_format_to_str(file_format: u8): *c.char {
    if (file_format === 1) {
        return s`Legacy`
    } else if (file_format === 2) {
        return s`WAL`
    } else {
        return s`Unknown`
    }
}
